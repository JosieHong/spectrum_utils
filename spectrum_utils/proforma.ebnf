%import common.DIGIT
%import common.INT
%import common.LETTER
%import common.NUMBER
%import common.SIGNED_INT
%import common.SIGNED_NUMBER
%import common.WS

// ProForma specification: https://github.com/HUPO-PSI/ProForma/
// Version: June 29, 2021
proforma: (peptidoform (CROSSLINK | CHIMERIC))* peptidoform
CROSSLINK: "//"
CHIMERIC: "+"

peptidoform: peptide ["/" charge]

peptide: mod_global* mod_labile* mod_unknown_pos? mod_n_term? (aa | mod_range)+ mod_c_term?
// TODO: Prefix modification order? https://github.com/HUPO-PSI/ProForma/issues/3
// TODO: Amino acid sequence ambiguity (section 4.7).

aa.10: AA [mod+ | ("[" mod_label "]")]
AA: LETTER

mod_global: "<" (ISOTOPE | (mod "@" (AA ",")* AA)) ">"
ISOTOPE: INT? LETTER+ SIGNED_INT?

mod_unknown_pos: mod+ "?"

mod:        "[" ((mod_name | mod_accession | mod_mass | mod_formula | mod_glycan | info) mod_label? "|")* (mod_name | mod_accession | mod_mass | mod_formula | mod_glycan | info) mod_label? "]" ("^" MOD_COUNT)?
mod_labile: "{" ((mod_name | mod_accession | mod_mass | mod_formula | mod_glycan | info)            "|")* (mod_name | mod_accession | mod_mass | mod_formula | mod_glycan | info)            "}" ("^" MOD_COUNT)?
MOD_COUNT: INT

mod_n_term: (mod | ("[" mod_label "]")) "-"
mod_c_term: "-" (mod | ("[" mod_label "]"))

mod_range: "(" mod_range_pos ")" mod+
mod_range_pos: aa+

mod_name.2: ((CV_ABBREV ":") | (CV_ABBREV_OPT ":")?) TEXT
CV_ABBREV_OPT: "U"i | "M"i
CV_ABBREV: "U"i | "M"i | "R"i | "X"i | "G"i

mod_accession.5: CV_NAME ":" TEXT
CV_NAME: "UNIMOD"i | "MOD"i | "RESID"i | "XLMOD"i | "GNO"i

mod_mass.5: [(CV_ABBREV | MOD_MASS_OBS) ":"] MOD_MASS
MOD_MASS_OBS: "Obs"i
MOD_MASS: ("+" | "-") NUMBER

mod_formula.5: "Formula:"i ("[" ISOTOPE "]")* FORMULA
FORMULA: (LETTER+ SIGNED_INT? WS?)+

mod_glycan.5: "Glycan:" glycan+
glycan: GLYCAN GLYCAN_COUNT?
// From: https://github.com/HUPO-PSI/ProForma/tree/master/monosaccharides
// Version: September 13, 2020
// Glycans have to be specified in reversed order to support greedy parsing.
GLYCAN: "sulfate"i | "phosphate"i | "en,a-Hex"i | "d-Hex"i | "a-Hex"i | "Tri"i
    | "Tet"i | "Sug"i | "Pen"i | "Oct"i | "Non"i | "Neu5Gc"i | "Neu5Ac"i
    | "Neu"i | "HexS"i | "HexP"i | "HexNS"i | "HexNAc(S)"i | "HexNAc"i
    | "HexN"i | "Hex"i | "Hep"i | "Fuc"i | "Dec"i
GLYCAN_COUNT: INT

info.5: "Info:"i TEXT

mod_label.3 : "#" (MOD_LABEL_XL | MOD_LABEL_BRANCH | MOD_LABEL) ["(" MOD_SCORE ")"]
MOD_LABEL_XL: "XL" MOD_LABEL
MOD_LABEL_BRANCH: "BRANCH"
MOD_LABEL: (LETTER | DIGIT)+
MOD_SCORE: SIGNED_NUMBER

charge: CHARGE ["[" ion "]"]
CHARGE: SIGNED_INT
ion: [TEXT ","] TEXT

TEXT: /.+/
